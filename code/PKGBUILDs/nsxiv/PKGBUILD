# Maintainer: Lucien Cartier-Tilet <lucien@phundrak.com>
# Original Maintainer: Sanjay Pavan <withercubes@protonmail.com>

_pkgname=nsxiv
pkgname="${_pkgname}-bepo-git"
pkgver=31.r19.gc03ec39
pkgrel=1
pkgdesc='New Simple X Image Viewer'
arch=('x86_64')
license=('GPL2')
conflicts=("$_pkgname" "${_pkgname}-git")
provides=('sxiv')
url="https://codeberg.org/${_pkgname}/${_pkgname}"
depends=('imlib2' 'desktop-file-utils' 'xdg-utils' 'hicolor-icon-theme' 'libexif' 'libxft' 'giflib' 'libwebp')
makedepends=(git)
source=("${_pkgname}::git+$url" config.h)
b2sums=('SKIP'
        '657b40dab4ed27e2d1402feb4fcd2dd225504c2e3a619da08cf87e36405d18a161a64939374670626e896c03a553a718f93b1bf54703f2d30dcdb27e082d6c21')

pkgver() {
  cd "$srcdir/${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${_pkgname}"
  [[ -f $srcdir/config.h ]] && cp $srcdir/config.h .
}

build() {
  make -C "$_pkgname"
}

package() {
  cd "$srcdir/${_pkgname}"
  make PREFIX=/usr DESTDIR="$pkgdir" install-all
  ln -s /usr/bin/nsxiv "${pkgdir}/usr/bin/sxiv"
}

# vim:set ts=2 sw=2 et:
